// Find reactions similar to a query reaction by structure fingerprint and return the protein
// sequences that encode for genes related to the reaction
// Args:
//   rid - reaction id to use as query origin
//   sf_sim - the minimum similarity of reactions as determined by structure fingerprint

let similar_rxn_ids = (
for e in rxn_similar_to_reaction
   filter e.sf_similarity >= @sf_sim
   filter e._to == @rid || e._from == @rid
   return e._to == @rid ? e._from : e._to
)

let similar_complex_ids = (
for e in rxn_reaction_within_complex
   filter e._from in similar_rxn_ids
   return e._to
)

let genes = FLATTEN(
for c in rxn_gene_complex
   filter c._id IN similar_complex_ids
   return c.genes
)

let sequences = (
for g in ncbi_gene
   filter g._key IN genes
   return g.dna_sequence
)

return {sequences: sequences, count: COUNT(sequences)}

